buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath 'com.palominolabs.gradle.task:gradle-git-clone-task:0.0.3'
    }
}

apply plugin: "net.minecraftforge.gradle.forge"

version = "snapshot_20190621"
group = modGroup
archivesBaseName = modBaseName



minecraft {
    version = "1.12.2-14.23.5.2768"
    runDir = "run"
    mappings = "snapshot_20180701"
    makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    replace "@VERSION@", project.version
}

repositories {
    mavenCentral()
    maven {
        name = "OpenComputers"
        url = "https://maven.cil.li/"
    }
    maven {
        url "https://maven.amadornes.com/" // for MCMultiPart
    }
}

configurations {
    extraLibs
    compileOnly.extendsFrom extraLibs
}

def openComputersVersion = "MC1.12.2-1.7.+"
def ae2Version = "rv6-stable-1"
def projredVersion="1.12.2-4.9.0.86"
def MCMPVersion = "2.5.3"
def rtfmVersion = "MC1.12-1.0.1.4"
def commons_version = "MC1.12.2-1.0.3"

dependencies {
    deobfCompile "li.cil.oc:OpenComputers:${openComputersVersion}"

    compileOnly "li.cil.manual:RTFM:${rtfmVersion}"

    deobfCompile "MCMultiPart2:MCMultiPart:${MCMPVersion}"

    compile fileTree(include: ['*.jar'], dir: 'libs')

    extraLibs files("libs/commons0815/Commons0815-${commons_version}-dev.jar")

    // all those just for the case block =>
    provided ("appeng:appliedenergistics2:${ae2Version}:api") {
        transitive = false
    }

    provided ("mrtjp:ProjectRed:${projredVersion}:Base") {
        exclude module: "NotEnoughItems"
        exclude module: "CodeChickenLib"
        exclude module: "jei_1.12"
        exclude module: "ForgeMultipart"
    }

}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

task flushWiki(type: Delete) {
    delete "src/main/resources/assets/ocdevices/doc"
}

task syncGitWiki(type: com.palominolabs.gradle.task.git.clone.GitCloneTask) {
    dir = file("src/main/resources/assets/ocdevices/doc")
    uri = 'https://github.com/ben-mkiv/OCDevices.wiki.git'
    treeish = "origin/master"
    forceFetch = true
    reset = true
}


jar {
    manifest {
        attributes([
                "Maven-Artifact":"${project.group}:${project.archivesBaseName}:${project.version}",
                'Timestamp': System.currentTimeMillis(),
                'ContainedDeps': "Commons0815-${commons_version}.jar"
        ])
    }

    from("libs/Commons0815-${commons_version}.jar"){
        into ("META-INF/libraries")
    }
}

syncGitWiki.dependsOn(flushWiki)
build.dependsOn(syncGitWiki)
